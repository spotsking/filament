material {
    name : tonemap,
    parameters : [
        {
            type : sampler2d,
            name : colorBuffer,
            precision: high
        },
        {
            type : sampler2d,
            name : bloomBuffer,
        },
        {
            type : int,
            name : dithering
        },
        {
            type : int,
            name : fxaa
        },
        {
            type : float,
            name : bloom
        }
    ],
    variables : [
        vertex
    ],
    depthWrite : false,
    depthCulling : false,
    domain: postprocess
}

vertex {
    void postProcessVertex(inout PostProcessVertexInputs postProcess) {
        postProcess.vertex.xy = postProcess.normalizedUV;
    }
}

fragment {

#include "../../../shaders/src/tone_mapping.fs"
#include "../../../shaders/src/conversion_functions.fs"
#include "../../../shaders/src/dithering.fs"
#include "../../../shaders/src/bloom.fs"

    vec3 resolveFragment(const ivec2 uv) {
        return texelFetch(materialParams_colorBuffer, uv, 0).rgb;
    }

    vec4 resolveAlphaFragment(const ivec2 uv) {
        return texelFetch(materialParams_colorBuffer, uv, 0);
    }

    vec4 resolve(inout PostProcessInputs postProcess) {
#if POST_PROCESS_OPAQUE
        vec4 color = vec4(resolveFragment(ivec2(getUV())), 1.0);
        if (materialParams.bloom > 0.0) {
            color.rgb  = bloom(postProcess, color.rgb);
        }
        color.rgb  = tonemap(color.rgb);
        color.rgb  = OECF(color.rgb);
        if (materialParams.fxaa > 0) {
            color.a = luminance(color.rgb);
        }
#else
        vec4 color = resolveAlphaFragment(ivec2(getUV()));
        color.rgb /= color.a + FLT_EPS;
        if (materialParams.bloom > 0.0) {
            color.rgb  = bloom(postProcess, color.rgb);
        }
        color.rgb  = tonemap(color.rgb);
        color.rgb  = OECF(color.rgb);
        color.rgb *= color.a + FLT_EPS;
#endif
        return color;
    }

    void postProcess(inout PostProcessInputs postProcess) {
        postProcess.color = resolve(postProcess);
        if (materialParams.dithering > 0) {
            postProcess.color = dither(postProcess.color);
        }
    }

}
